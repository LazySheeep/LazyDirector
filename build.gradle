plugins {
    id 'com.gradleup.shadow' version '8.3.2'
    id 'java'
}

group = 'io.lazysheeep'
version = '0.1.0'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    implementation "org.spongepowered:configurate-hocon:4.1.2"
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
}

shadowJar {
    relocate 'co.aikar.commands', 'io.lazysheeep.lazydirector.acf'
    relocate 'co.aikar.locales', 'io.lazysheeep.lazydirector.locales'
    dependencies {
        exclude(dependency('com.github.LazySheeep:LazuliUI'))
    }
    mergeServiceFiles()
}

build.dependsOn shadowJar

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

task copyJar(type: Copy) {
    from shadowJar.archiveFile
    into file("$rootDir/test/paper server 1.21/plugins")
}

task copyConfig(type: Copy) {
    from file("$rootDir/src/main/resources/example.conf")
    into file("$rootDir/test/paper server 1.21/plugins/LazyDirector")
}

jar.finalizedBy(copyJar, copyConfig)
