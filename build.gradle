plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group = 'io.lazysheeep'
version = '1.0-SNAPSHOT'

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-20240805.181000-127"
    implementation "org.spongepowered:configurate-hocon:4.1.2"
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
}

shadowJar {
    relocate 'co.aikar.commands', 'io.lazysheeep.lazydirector.acf'
    relocate 'co.aikar.locales', 'io.lazysheeep.lazydirector.locales'
}

build.dependsOn shadowJar

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

task copyJar(type: Copy) {
    from jar
    into file("$rootDir/test/paper server 1.21/plugins")
}

task copyConfig(type: Copy) {
    from file("$rootDir/src/main/resources/config.yml")
    into file("$rootDir/test/paper server 1.21/plugins/LazyDirector")
}

jar.finalizedBy(copyJar, copyConfig)
